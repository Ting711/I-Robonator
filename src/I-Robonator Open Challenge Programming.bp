folder "prjs""Clever"
Speaker.Tone(100,1200,80)
Sensor.SetMode(1,0)
Sensor.SetMode(3,0)

Function waitDegrees(in number degrees)
  dDegrees=Motor.GetCount("D")
  While Math.Abs(Motor.GetCount("D")-dDegrees)<degrees
    Steering()
  EndWhile
  Speaker.Tone(100,4000,50)
EndFunction

Function setSensorMode(in number port, in number chanel, in number mode)
  address = 80 + 1 * (chanel - 1)
  Sensor.WriteI2CRegister(port,address,82,mode)
EndFunction

Function getValue(in number port, in number chanel, out number values)
  address = 80 + 1 * (chanel - 1)
  readData = Sensor.ReadI2CRegisters(port,address,84,2)
  values = readData[1] * 256 + readData[0]
  LCD.Clear()
  LCD.Text(1,0,0,2,values)
EndFunction

target=0
loopCount=0
cw=0
gyroLastError=0
wallError=0
wallLastError=0


setSensorMode(2,1,0)
setSensorMode(2,2,0)
getValue(2,1,leftDistance)
getValue(2,2,rightDistance)

Sub Steering
  If leftDistance>0 And rightDistance>0 Then
    Motor.StartPower("D",90)
  Else
    Motor.StartPower("D",0)
  EndIf
  
  relativeHeading = Sensor.ReadRawValue(3,0)*-1 + target
  
  If Math.Abs(relativeHeading)<15 And loopCount > 0 Then
    If cw=1 Then
      getValue(2,2,rightDistance)
      If rightDistance<2500 Then
        Wall=rightDistance
      EndIf
    Else
      getValue(2,1,leftDistance)
      If leftDistance<2500 Then
        Wall=leftDistance
      EndIf
    EndIf
    
    'Gyro Correction
    gyroCorrection = relativeHeading * 0.8 + (relativeHeading-gyroLastError) * 10
    
    'Wall Correction
    wallError= (190 - Wall) * cw
    wallCorrection = wallError * 0.07 + (wallError-wallLastError) * 0.5
    
    'Steering Correction
    turn = gyroCorrection + wallCorrection
    
    'Save Last Error
    gyroLastError=relativeHeading
    wallLastError=wallError
  Else
    turn = relativeHeading * 1.5
  EndIf
  
  'Power To Medium Motor
  steeringPower = (turn - Motor.GetCount("A")) * 1.5
  Motor.StartPower("A", steeringPower)
EndSub

getValue(2,1,leftDistance)
getValue(2,2,rightDistance)

Motor.StartPower("A",50)
  Program.Delay(500)
  While Motor.GetSpeed("A")<>0
    'wait
  EndWhile
  Motor.Move("A",-50,38,"True")
  Motor.ResetCount("A")
  Motor.StartPower("D",100)

While loopCount<12
  If loopCount=0 Then
    waitDegrees(150)
    While leftDistance<2000 And rightDistance<2000
      getValue(2,1,leftDistance)
      getValue(2,2,rightDistance)
      Steering()
    EndWhile
    If rightDistance>2000 Then
      cw=1
    Else
      cw=-1
    EndIf
    
  Else
    waitDegrees(1250)
    
    If cw=1 Then
      While rightDistance<1200
        Steering()
      EndWhile
    Else
      While leftDistance<1200
        Steering()
      EndWhile
    EndIf
  EndIf
  target = target - 89.25 * cw
  loopCount = loopCount + 1
  Speaker.Tone(100,220,50)
EndWhile

    LCD.Text(1,50,0,2,leftDistance)
    LCD.Text(1,50,20,2,rightDistance)

waitDegrees(1350)
Motor.Stop("D", "True")

While 1=1
  Motor.StartPower("D",0)
EndWhile
